plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id "dev.architectury.loom" version "1.10-SNAPSHOT"
    id 'me.modmuss50.mod-publish-plugin' version '0.8.+'
    id 'net.minecraftforge.gradleutils' version "2.+"
}

version = gitversion.tagOffset
group = mod_group_id

base {
    archivesName = mod_id
}

// Mojang ships Java 21 to end users in 1.20.5+, so your mod should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
println "Version: ${version}"


// Include resources generated by data generators.
sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    // Put repositories for dependencies here
    // ForgeGradle automatically adds the Forge maven for you

    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html#sub:flat_dir_resolver
    // flatDir {
    //     dir 'libs'
    // }

    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { url = 'https://maven.neoforged.net/releases/' }
    maven { url = "https://maven.createmod.net" } // Create, Ponder, Flywheel
    maven { url = "https://mvn.devos.one/snapshots" } // Registrate
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" } // ForgeConfigAPIPort
    maven { url = "https://maven.blamejared.com" } // JEI, Vazkii's Mods
    maven { url = "https://maven.liukrast.net/" } // extra gauges
}

loom {
    silentMojangMappingsLicense()
    runs {
        configureEach {
            property("forge.logging.markers", "")
            property("forge.logging.console.level", "debug")
        }

        data {
            data()
            programArgs "--all", "--mod", "${mod_id}"
            programArgs "--existing", file("src/main/resources").absolutePath
            programArgs "--output", file('src/generated/resources/').absolutePath
        }
    }
}

dependencies {
    minecraft "net.minecraft:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()

    neoForge "net.neoforged:${forge_flavour}:${forge_version}"

    // Example mod dependency with JEI - using fg.deobf() ensures the dependency is remapped to your development mappings
    // The JEI API is declared for compile time use, while the full JEI artifact is used at runtime
    modCompileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    modCompileOnly "mezz.jei:jei-${minecraft_version}-${forge_flavour}-api:${jei_version}"
    modCompileOnly "mezz.jei:jei-${minecraft_version}-${forge_flavour}:${jei_version}"
    modRuntimeOnly "mezz.jei:jei-${minecraft_version}-${forge_flavour}:${jei_version}"

    // Example mod dependency using a mod jar from ./libs with a flat dir repository
    // This maps to ./libs/coolmod-${minecraft_version}-${coolmod_version}.jar
    // The group id is ignored when searching -- in this case, it is "blank"
    // implementation fg.deobf("blank:coolmod-${minecraft_version}:${coolmod_version}")

    // For more info:
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

    modImplementation "com.simibubi.create:create-${minecraft_version}:${create_version}:slim", { transitive = false }
    modImplementation "net.createmod.ponder:Ponder-${forge_flavour_pascal}-${minecraft_version}:${ponder_version}"
    modCompileOnly "dev.engine-room.flywheel:flywheel-${forge_flavour}-api-${minecraft_version}:${flywheel_version}"
    modRuntimeOnly "dev.engine-room.flywheel:flywheel-${forge_flavour}-${minecraft_version}:${flywheel_version}"
    modImplementation "com.tterrag.registrate:Registrate:${registrate_version}"
    modCompileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.0-rc.3"))
    include(implementation("io.github.llamalad7:mixinextras-${forge_flavour}:0.5.0-rc.3")) {
        version {
            strictly '[0.5.0-rc.3,)'
        }
    }

    include(implementation(project(":CreateFactoryAbstractions"))) {
        transitive = false
    }

    if (!Boolean.getBoolean('idea.sync.active')) {
//        annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
    }

//    modRuntimeOnly "net.liukrast:extra_gauges-${minecraft_version}:1.1.1", {
//        transitive = false
//    }

    // modRuntimeOnly "curse.maven:create-vibrant-vaults-1251719:6463890"
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
processResources {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version: forge_version, forge_flavour: forge_flavour, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: version,
            mod_authors: mod_authors, mod_description: mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }

    if (forge_flavour != 'forge') {
        rename 'mods\\.toml', "${forge_flavour}.mods.toml"
    }
}

// Example for how to get properties into the manifest for reading at runtime.
tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConfigs'            : 'mixins.create_factory_logistics.json'
        ])
    }
}

publishMods {
    file = jar.archiveFile
    changelog = ''
    type = BETA
    displayName = "$mod_name $project.version"
    modLoaders.add(forge_flavour)

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        projectId = "1218807"
        minecraftVersions.add(minecraft_version)
    }

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "QMGO17gm"
        minecraftVersions.add(minecraft_version)
    }

    dryRun = providers.environmentVariable("MODRINTH_TOKEN").getOrNull() == null
}

tasks.named('publishMods').configure {
    dependsOn 'assemble'
}

allprojects {
    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
    }
}
